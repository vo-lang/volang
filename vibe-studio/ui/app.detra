// Vibe Studio - IDE Interface
// Using Detra Semantic Components

state App {
    activePanel string = "files"
    activeBottomTab string = "terminal"
    showBottom bool = true
    external currentFile string
    external editorContent string
    external fileTree string
    external terminalOutput string
}

command OpenFile(path string)
command SaveFile(path string, content string)
command RunFile(path string)
command ClearTerminal()

action SetPanel(id string) {
    set state.activePanel = id
}

action SetBottomTab(id string) {
    set state.activeBottomTab = id
}

action ToggleBottom() {
    set state.showBottom = !state.showBottom
}

action UpdateContent(content string) {
    set state.editorContent = content
}

action DoOpenFile(path string) {
    emit OpenFile(path: path)
}

action DoSave() {
    emit SaveFile(path: state.currentFile, content: state.editorContent)
}

action DoRun() {
    set state.showBottom = true
    set state.activeBottomTab = "terminal"
    emit RunFile(path: state.currentFile)
}

action DoClear() {
    emit ClearTerminal()
}

view Main {
    AppShell(showBottomPanel: state.showBottom) {
        ActivityBar(active: state.activePanel, onSelect: SetPanel(id: $id))
        
        Sidebar(width: 220) {
            switch state.activePanel {
            case "files":
                FileTree(selected: state.currentFile, onSelect: DoOpenFile(path: $path))
            case "search":
                SearchPanel()
            default:
                Column(padding: 16) {
                    Text(text: "Settings", size: 14, bold: true)
                }
            }
        }
        
        MainArea() {
            TabBar(active: state.currentFile)
            CodeEditor(content: state.editorContent, language: "vo", onChange: UpdateContent(content: $content))
        }
        
        BottomPanel(height: 180) {
            TabBar(active: state.activeBottomTab, onSelect: SetBottomTab(id: $id), onClose: ToggleBottom())
            Terminal(output: state.terminalOutput, onClear: DoClear())
        }
        
        StatusBar() {
            StatusBarItem(text: "Ln 1, Col 1")
            StatusBarItem(text: "UTF-8", align: "right")
            StatusBarItem(text: "Vo", align: "right")
        }
    }
}
